grammar hu.bme.mit.kk.dsl.Language with org.eclipse.xtext.common.Terminals

generate language "http://www.bme.hu/mit/kk/dsl/Language"

Model:
	people+=Person ((',' people+=Person)* 'and' people+=Person) 'are on the island.'
	statements+=(Statement)+;

Person:
	name=ID;

Statement:
	person=[Person] 'says that' expression=Expression '.';

Expression:
	Iff;

Iff returns Expression:
	Or ({Iff.left=current} 'if and only if' right=Or)?;

Or returns Expression:
	And ({Or.left=current} 'or' right=And)*;

And returns Expression:
	Negationable ({And.left=current} 'and' right=Negationable)*;

Negationable returns Expression:
	'but'? ('it is not true that' {Negationable} value=Primary | KnaveReference | Primary);

Primary returns Expression:
	KnightReference |
	'(' Iff ')';

KnightReference returns Expression:
	{KnightReference} person=[Person] 'is a knight';
	
KnaveReference returns Expression:
	{KnaveReference} person=[Person] 'is a knave';
